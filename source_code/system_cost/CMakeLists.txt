# - Set allows giving a value to a variable similar to: UI_FILES = "file1.ui file2.ui file3.ui"
# - A list of .ui Files is made to make it easier to work with them
# - Glob can also be used but this manual way is safer and also the recommened approach for faster in between builds
# - The UI files need to be wrapped to generate the ui-xxxx.h files this is done using qt_wrap_ui
set(UI_FILES
	source_code/gui/CostGui_Alternative_Ecn_Cost_Dia.ui
)

# - Set allows giving a value to a variable similar to: H_FILES = "file1.h file2.h file3.h"
# - A list of .h Files is made to make it easier to work with them
# - All the h files in this library need to be added here MANUALLY.
# - This variable is passed to the library when creating it so that it knows what files are parts of it
set(H_FILES
	source_code/Cost_System.h
	source_code/Cost_Headers_Precompiled.h
	source_code/gui/CostGui_Alternative_Ecn_Cost_Dia.h
	source_code/Cost_Ecn_Cost_System.h
)

# - Set allows giving a value to a variable similar to: CPP_FILES = "file1.cpp file2.cpp file3.cpp"
# - A list of h Files is made to make it easier to work with them
# - All the .cpp files in this library need to be added here MANUALLY.
# - This variable is passed to the library when creating it so that it knows what files to compile
set(CPP_FILES
	source_code/Cost_System.cpp
	source_code/gui/CostGui_Alternative_Ecn_Cost_Dia.cpp
	source_code/Cost_Ecn_Cost_System.cpp
)

# - Set allows giving a value to a variable similar to: PrecompiledHeaders = "PrecompiledHeaders.h"
# - Set is used here for consistency of the code
# - The precompiledHeader will be targeted by the library so that it precomiples this header file for faster build times
set(PrecompiledHeaders
	source_code/Cost_Headers_Precompiled.h
)

# - add_library create the library
# - The add_library is the engine here and is defined as add_library(<name> [STATIC | SHARED | MODULE] [EXCLUDE_FROM_ALL] [<source>...])
# - system_cost is the name given to the library
# - What follows are the source codes
add_library(system_cost
	${UI_FILES}
	${H_FILES}
	${CPP_FILES}
	${PrecompiledHeaders}
)

# - target_precompile_headers sets the precompiled headers of the target (library)
# - The precompiled headers is set to PUBLIC so that other libraries in the solution can also use this precompiled header
target_precompile_headers(system_cost PUBLIC ${PrecompiledHeaders})

# - target_include_directories sets the include directories for a specfic target
# - It is set to PUBLIC meaning the parent targerts also inherit these directories
# - For example: the HYD Library is a parent of solver and has access to things like: include "cvode.h"
# - ${CMAKE_CURRENT_BINARY_DIR} because it is the location of the ui_xxxx.h files that are generated by qt_wrap_ui
# - Cost includes headers from alt and thus the locations of these headers are also included
target_include_directories(system_cost
    PUBLIC
        ${CMAKE_CURRENT_BINARY_DIR}
		source_code
		source_code/gui
		${CMAKE_SOURCE_DIR}/source_code/system_alt/source_code
		${CMAKE_SOURCE_DIR}/source_code/system_alt/source_code/gui
)

# - target_link_libraries links libraries to a specfic target
# - The target_include_directories tells the program where the headers are. But not what the classes do
# - The dlls or libs are what include the functions themselves.
# - system_cost only depends on system_risk, the rest are for clarity
target_link_libraries(system_cost system_risk system_dam system_fpl system_hydraulic system_sys)

###
### Yes, AUTOUIC is on, but for some reason we still need to do this
###
### We already wraped them up

## Creating a custom target for libraries that depend on headers from this
qt_wrap_ui(UI_FILES ${UI_FILES})
