/*
 * HIGH-PERFORMANCE INTEGRATED MODELLING SYSTEM (HiPIMS)
 * Copyright (C) 2023 Luke S. Smith, Qiuhua Liang.
 * This code is licensed under GPLv3. See LICENCE for more information.
 * This file has been modified by Alaa Mroue on 04.2023-12.2023.
 * See OriginalSourceCode.zip for changes. (Based on 1e62acf6b9b480e08646b232361b68c1827d91ae from https://github.com/lukeshope/hipims-ocl )
 */

/*
 *  Reconstruct the cell data in a non-negative way (depth positivity preserving)
 */
cl_uchar reconstructInterface(
	cl_double4		pStateLeft,						// Left current state		Z, Zmax, Qx, Qy
	cl_double		dBedLeft,						// Left bed elevation
	cl_double4		pStateRight,					// Right current state
	cl_double		dBedRight,						// Right bed elevation
	cl_double8*		pOutputLeft,					// Output data for LHS of Riemann
	cl_double8*		pOutputRight,					// Output data for RHS of Riemann
	cl_uchar		ucDirection						// Direction under consideration
	)
{
	cl_uchar		ucStop = 0;
	cl_double8		pReconstructionLeft, pReconstructionRight;
	cl_double		dDepthL = pStateLeft.x - dBedLeft;
	cl_double		dDepthR = pStateRight.x - dBedRight;

	// Initial values before reconstruction
	pReconstructionLeft = (cl_double8)
		{pStateLeft.S0,																		// Z	S0   opt_s
		dDepthL,																			// H	S1   opt_h
		pStateLeft.S2,																		// Qx	S2   Q_x
		pStateLeft.S3,																		// Qy	S3   Q_y
		(dDepthL < VERY_SMALL ? 0.0 : pStateLeft.S2 / dDepthL),								// U	S4   v_x
		(dDepthL < VERY_SMALL ? 0.0 : pStateLeft.S3 / dDepthL),								// V	S5	 v_y
		dBedLeft,																			// Zb	S6	 opt_z
		0.0};																				//		S7
	pReconstructionRight = (cl_double8)
		{pStateRight.S0,																	// Z	S0
		dDepthR,																			// H	S1
		pStateRight.S2,																		// Qx	S2
		pStateRight.S3,																		// Qy	S3
		(dDepthR < VERY_SMALL ? 0.0 : pStateRight.S2 / dDepthR),							// U	S4
		(dDepthR < VERY_SMALL ? 0.0 : pStateRight.S3 / dDepthR),							// V	S5
		dBedRight,																			// Zb	S6
		0.0};																				//		S7

	// Maximum bed elevation and vertical shift factor
	cl_double	dBedMaximum = (pReconstructionLeft.S6 > pReconstructionRight.S6 ? pReconstructionLeft.S6 : pReconstructionRight.S6);
	cl_double	dShiftV = dBedMaximum - (ucDirection < DOMAIN_DIR_S ? pStateLeft : pStateRight).S0;
	if (dShiftV < 0.0) dShiftV = 0.0;

	// Adjustment of depths and dependent elements
	pReconstructionLeft.S1 = (pStateLeft.S0 - dBedMaximum > 0.0 ? (pStateLeft.S0 - dBedMaximum) : 0.0);
	pReconstructionLeft.S0 = pReconstructionLeft.S1 + dBedMaximum;
	pReconstructionLeft.S2 = pReconstructionLeft.S1 * pReconstructionLeft.S4;
	pReconstructionLeft.S3 = pReconstructionLeft.S1 * pReconstructionLeft.S5;

	pReconstructionRight.S1 = (pStateRight.S0 - dBedMaximum > 0.0 ? (pStateRight.S0 - dBedMaximum) : 0.0);
	pReconstructionRight.S0 = pReconstructionRight.S1 + dBedMaximum;
	pReconstructionRight.S2 = pReconstructionRight.S1 * pReconstructionRight.S4;
	pReconstructionRight.S3 = pReconstructionRight.S1 * pReconstructionRight.S5;

	// Prevent draining from a dry cell
	// and the stopping conditions
	switch (ucDirection)
	{
	case DOMAIN_DIR_N:

		// NOTE: Do NOT include zero velocity in the check. Velocity must be non-negative for stopping conditions
		//		 to be required.
		if (pReconstructionLeft.S1 <= VERY_SMALL && pStateLeft.w > 0.0) { ucStop++; }
		if (pReconstructionRight.S1 <= VERY_SMALL && pReconstructionLeft.S5 < 0.0)  { ucStop++; pReconstructionLeft.S5 = 0.0; }
		if (pReconstructionLeft.S1 <= VERY_SMALL && pReconstructionRight.S5 > 0.0) { ucStop++; pReconstructionRight.S5 = 0.0; }

		break;
	case DOMAIN_DIR_S:

		if (pReconstructionRight.S1 <= VERY_SMALL && pStateRight.w < 0.0) { ucStop++; }
		if (pReconstructionRight.S1 <= VERY_SMALL && pReconstructionLeft.S5 < 0.0)  { ucStop++; pReconstructionLeft.S5 = 0.0; }
		if (pReconstructionLeft.S1 <= VERY_SMALL  && pReconstructionRight.S5 > 0.0) { ucStop++; pReconstructionRight.S5 = 0.0; }

		break;
	case DOMAIN_DIR_E:

		if (pReconstructionLeft.S1 <= VERY_SMALL  && pStateLeft.z > 0.0) { ucStop++; }
		if (pReconstructionRight.S1 <= VERY_SMALL && pReconstructionLeft.S4 < 0.0)  { ucStop++; pReconstructionLeft.S4 = 0.0; }
		if (pReconstructionLeft.S1 <= VERY_SMALL  && pReconstructionRight.S4 > 0.0) { ucStop++; pReconstructionRight.S4 = 0.0; }

		break;
	case DOMAIN_DIR_W:

		if (pReconstructionRight.S1 <= VERY_SMALL && pStateRight.z < 0.0) { ucStop++; }
		if (pReconstructionRight.S1 <= VERY_SMALL && pReconstructionLeft.S4 < 0.0)  { ucStop++; pReconstructionLeft.S4 = 0.0; }
		if (pReconstructionLeft.S1 <= VERY_SMALL  && pReconstructionRight.S4 > 0.0) { ucStop++; pReconstructionRight.S4 = 0.0; }

		break;
	}

	// Local modification of the bed level (and consequently, FSL to maintain depth)
	pReconstructionLeft.S6 = dBedMaximum - dShiftV;
	pReconstructionRight.S6 = dBedMaximum - dShiftV;
	pReconstructionLeft.S0 -= dShiftV;
	pReconstructionRight.S0 -= dShiftV;

	// Stopping conditions
	/*/
	if (   pReconstructionLeft.S1 < VERY_SMALL &&
	( ( ucDirection == DOMAIN_DIR_N && pStateLeft.w > 0.0 ) ||
	( ucDirection == DOMAIN_DIR_E && pStateLeft.z > 0.0 ) ) )
	ucStop++;
	if (   pReconstructionRight.S1 < VERY_SMALL &&
	( ( ucDirection == DOMAIN_DIR_S && pStateRight.w < 0.0 ) ||
	( ucDirection == DOMAIN_DIR_W && pStateRight.z < 0.0 ) ) )
	ucStop++;
	*/

	// Output vector: Z, H, Qx, Qy, U, V, Zb
	*pOutputLeft = pReconstructionLeft;
	*pOutputRight = pReconstructionRight;

	// Stop flow?
	return ucStop;
}

/*
 *  Calculate everything without using LDS caching
 */
__kernel REQD_WG_SIZE_FULL_TS
void gts_cacheDisabled ( 
			__constant	cl_double *  				dTimestep,						// Timestep
			__global	cl_double const * restrict	dBedElevation,					// Bed elevation
			__global	cl_double4 *  			pCellStateSrc,					// Current cell state data
			__global	cl_double4 *  			pCellStateDst,					// Current cell state data
			__global	cl_double const * restrict	dManning,						// Manning values
			__global	sUsePolini const * restrict	pUsePoleni,					// Switch to use Poleni
			__global	cl_double const * restrict	pOpt_zxmax,					// 	
			__global	cl_double const * restrict	pOpt_zymax					// 	
		)
{

	// Identify the cell we're reconstructing (no overlap)
	__private cl_long					lIdxX			= get_global_id(0);
	__private cl_long					lIdxY			= get_global_id(1);
	__private cl_ulong					ulIdx, ulIdxNeig;
	__private cl_uchar					ucDirection;
	
	ulIdx = getCellID(lIdxX, lIdxY);

	// Don't bother if we've gone beyond the domain bounds
	if ( lIdxX > DOMAIN_COLS - 1 || 
		 lIdxY > DOMAIN_ROWS - 1 || 
		 lIdxX < 0 || 
		 lIdxY < 0 ) 
		return;

	__private cl_double		dLclTimestep	= *dTimestep;
	__private cl_double		dManningCoef;
	__private cl_double		dCellBedElev,dNeigBedElevN,dNeigBedElevE,dNeigBedElevS,dNeigBedElevW;
	__private cl_double		dNeigBedElevN1,dNeigBedElevE1,dNeigBedElevS1,dNeigBedElevW1;
	__private cl_double4	pCellData,pNeigDataN,pNeigDataE,pNeigDataS,pNeigDataW;					// Z, Zmax, Qx, Qy
	__private cl_double4	pSourceTerms,		dDeltaValues;										// Z, Qx, Qy
	__private cl_double4	pFlux[4];																// Z, Qx, Qy
	__private cl_double8	pLeft,				pRight;												// Z, H, Qx, Qy, U, V, Zb
	__private cl_uchar		ucStop			= 0;
	__private cl_uchar		ucDryCount		= 0;
	//Poleni
	__private sUsePolini	pUsePoleniFlags;
	__private bool			usePoleniN, usePoleniE, usePoleniS, usePoleniW;
	__private cl_double		dOpt_zxmax,dOpt_zymax,dOpt_znxmax,dOpt_znymax;
	

	// Also don't bother if we've gone beyond the total simulation time
	if (dLclTimestep <= 0.0)
	{
		// TODO: Is there a way of avoiding this?!
		pCellStateDst[ulIdx] = pCellStateSrc[ulIdx];
		return;
	}

	// Load cell data
	dCellBedElev		= dBedElevation[ ulIdx ];
	pCellData			= pCellStateSrc[ ulIdx ];
	dManningCoef		= dManning[ ulIdx ];

	cl_double dCellBedElev_original =  dCellBedElev;
	dOpt_zxmax			= pOpt_zxmax[ ulIdx ];
	dOpt_zymax			= pOpt_zymax[ ulIdx ];
	pUsePoleniFlags		= pUsePoleni[ ulIdx ];
	usePoleniN			= pUsePoleniFlags.usePoliniN;
	usePoleniE			= pUsePoleniFlags.usePoliniE;
	usePoleniS			= pUsePoleniFlags.usePoliniS;
	usePoleniW			= pUsePoleniFlags.usePoliniW;

	// Cell disabled?
	if ( dCellBedElev == -9999.0 )
	{
		pCellStateDst[ ulIdx ] = pCellData;
		return;
	}

	ucDirection = DOMAIN_DIR_W;
	ulIdxNeig = getNeighbourByIndices(lIdxX, lIdxY, ucDirection);
	dNeigBedElevW	= dBedElevation [ ulIdxNeig ];
	dNeigBedElevW1	= dBedElevation [ ulIdxNeig ];
	pNeigDataW		= pCellStateSrc	[ ulIdxNeig ];
	dOpt_znxmax		= pOpt_zxmax[ ulIdxNeig ];
	ucDirection = DOMAIN_DIR_S;
	ulIdxNeig = getNeighbourByIndices(lIdxX, lIdxY, ucDirection);
	dNeigBedElevS	= dBedElevation [ ulIdxNeig ];
	dNeigBedElevS1	= dBedElevation [ ulIdxNeig ];
	pNeigDataS		= pCellStateSrc	[ ulIdxNeig ];
	dOpt_znymax		= pOpt_zymax[ ulIdxNeig ];
	ucDirection = DOMAIN_DIR_N;
	ulIdxNeig = getNeighbourByIndices(lIdxX, lIdxY, ucDirection);
	dNeigBedElevN	= dBedElevation [ ulIdxNeig ];
	dNeigBedElevN1	= dBedElevation [ ulIdxNeig ];
	pNeigDataN		= pCellStateSrc	[ ulIdxNeig ];
	ucDirection = DOMAIN_DIR_E;
	ulIdxNeig = getNeighbourByIndices(lIdxX, lIdxY, ucDirection);
	dNeigBedElevE	= dBedElevation [ ulIdxNeig ];
	dNeigBedElevE1	= dBedElevation [ ulIdxNeig ];
	pNeigDataE		= pCellStateSrc	[ ulIdxNeig ];
	

	if(lIdxX == DOMAIN_COLS - 1){
		dNeigBedElevE = -9999.0;
	} 
	if( lIdxY == DOMAIN_ROWS - 1) {
		dNeigBedElevN = -9999.0;
	}
	if( lIdxX == 0) {
		dNeigBedElevW = -9999.0;
	}
	if( lIdxY == 0) {
		dNeigBedElevS = -9999.0;
	}

	//if (ulIdx == 1057){
	//	printf("Before:\n");
	//	printf("lIdxX: %f lIdxY: %f DOMAIN_COLS: %f DOMAIN_ROWS %f\n", lIdxX*1.0, lIdxY*1.0, DOMAIN_COLS*1.0, DOMAIN_ROWS*1.0);
	//	printf("C: opt_h: %f opt_z: %f v_x: %f v_y %f\n", pCellData.x, dCellBedElev, pCellData.z, pCellData.w);
	//	printf("N: opt_h: %f opt_z: %f v_x: %f v_y %f\n", pNeigDataN.x, dNeigBedElevN, pNeigDataN.z, pNeigDataN.w);
	//	printf("E: opt_h: %f opt_z: %f v_x: %f v_y %f\n", pNeigDataE.x, dNeigBedElevE, pNeigDataE.z, pNeigDataE.w);
	//	printf("S: opt_h: %f opt_z: %f v_x: %f v_y %f\n", pNeigDataS.x, dNeigBedElevS, pNeigDataS.z, pNeigDataS.w);
	//	printf("W: opt_h: %f opt_z: %f v_x: %f v_y %f\n", pNeigDataW.x, dNeigBedElevW, pNeigDataW.z, pNeigDataW.w);
	//}

	if ( pCellData.x  - dCellBedElev  < VERY_SMALL || dCellBedElev == -9999.0 ) ucDryCount++;
	if ( pNeigDataN.x - dNeigBedElevN < VERY_SMALL || dNeigBedElevN == -9999.0 ) ucDryCount++;
	if ( pNeigDataE.x - dNeigBedElevE < VERY_SMALL || dNeigBedElevE == -9999.0 ) ucDryCount++;
	if ( pNeigDataW.x - dNeigBedElevW < VERY_SMALL || dNeigBedElevW == -9999.0 ) ucDryCount++;
	if ( pNeigDataS.x - dNeigBedElevS < VERY_SMALL || dNeigBedElevS == -9999.0 ) ucDryCount++;

	// All neighbours are dry? Don't bother calculating
	if ( ucDryCount >= 5 ){
	 return;
	}

	// Reconstruct interfaces
		
	// -> North
	poliniAdjuster(usePoleniN, pCellData.x, pNeigDataN.x, dOpt_zymax, &dCellBedElev, &dNeigBedElevN, &pNeigDataN, DOMAIN_DIR_N);
	if(pNeigDataN.x < dNeigBedElevN){pNeigDataN.x = dNeigBedElevN;}

	if( dNeigBedElevN == -9999.0 || pNeigDataN.x==-9990.0) {
		pNeigDataN.x = pCellData.x+1.0;
		pNeigDataN.z = 0.0;
		pNeigDataN.w = 0.0;
		dNeigBedElevN = pCellData.x+1.0;
	}

	ucStop += reconstructInterface(
		pCellData,							// Left cell data
		dCellBedElev,						// Left bed elevation
		pNeigDataN,							// Right cell data
		dNeigBedElevN,						// Right bed elevation
		&pLeft,								// Output for left
		&pRight,							// Output for right
		DOMAIN_DIR_N
	);
	pNeigDataN.x  = pRight.S0;
	dNeigBedElevN = pRight.S6;
	pFlux[DOMAIN_DIR_N] = riemannSolver( DOMAIN_DIR_N, pLeft, pRight, false );

	dCellBedElev = dCellBedElev_original;
	
	
	// -> South
	poliniAdjuster(usePoleniS, pCellData.x, pNeigDataS.x, dOpt_znymax, &dCellBedElev, &dNeigBedElevS, &pNeigDataS, DOMAIN_DIR_S);
	if(pNeigDataS.x < dNeigBedElevS){pNeigDataS.x = dNeigBedElevS;}
	if( dNeigBedElevS == -9999.0 || pNeigDataS.x==-9990.0) {
		pNeigDataS.x = pCellData.x+1.0;
		pNeigDataS.z = 0.0;
		pNeigDataS.w = 0.0;
		dNeigBedElevS = pCellData.x+1.0;
	}
	ucStop += reconstructInterface(
		pNeigDataS,							// Left cell data
		dNeigBedElevS,						// Left bed elevation
		pCellData,							// Right cell data
		dCellBedElev,						// Right bed elevation
		&pLeft,								// Output for left
		&pRight,							// Output for right
		DOMAIN_DIR_S
	);
	pNeigDataS.x  = pLeft.S0;
	dNeigBedElevS = pLeft.S6;
	pFlux[DOMAIN_DIR_S] = riemannSolver( DOMAIN_DIR_S, pLeft, pRight, false );
	
	dCellBedElev = dCellBedElev_original;

	
	// -> East
	poliniAdjuster(usePoleniE, pCellData.x, pNeigDataE.x, dOpt_zxmax, &dCellBedElev, &dNeigBedElevE, &pNeigDataE, DOMAIN_DIR_E);
	if(pNeigDataE.x < dNeigBedElevE){pNeigDataE.x = dNeigBedElevE;}
	if( dNeigBedElevE == -9999.0 || pNeigDataE.x==-9990.0) {
		pNeigDataE.x = pCellData.x+1.0;
		pNeigDataE.z = 0.0;
		pNeigDataE.w = 0.0;
		dNeigBedElevE = pCellData.x+1.0;
	}
	ucStop += reconstructInterface(
		pCellData,							// Left cell data
		dCellBedElev,						// Left bed elevation
		pNeigDataE,							// Right cell data
		dNeigBedElevE,						// Right bed elevation
		&pLeft,								// Output for left
		&pRight,							// Output for right
		DOMAIN_DIR_E
	);
	pNeigDataE.x  = pRight.S0;
	dNeigBedElevE = pRight.S6;
	pFlux[DOMAIN_DIR_E] = riemannSolver( DOMAIN_DIR_E, pLeft, pRight, false );
	
	dCellBedElev = dCellBedElev_original;
	
	// -> West
	poliniAdjuster(usePoleniW, pCellData.x, pNeigDataW.x, dOpt_znxmax, &dCellBedElev, &dNeigBedElevW, &pNeigDataW, DOMAIN_DIR_W);
	if(pNeigDataW.x < dNeigBedElevW){pNeigDataW.x = dNeigBedElevW;}
	if( dNeigBedElevW == -9999.0 || pNeigDataW.x==-9990.0) {
		pNeigDataW.x = pCellData.x+1.0;
		pNeigDataW.z = 0.0;
		pNeigDataW.w = 0.0;
		dNeigBedElevW = pCellData.x+1.0;
	}
	ucStop += reconstructInterface(
		pNeigDataW,							// Left cell data
		dNeigBedElevW,						// Left bed elevation
		pCellData,							// Right cell data
		dCellBedElev,						// Right bed elevation
		&pLeft,								// Output for left
		&pRight,							// Output for right
		DOMAIN_DIR_W
	);
	pNeigDataW.x  = pLeft.S0;
	dNeigBedElevW = pLeft.S6;
	pFlux[DOMAIN_DIR_W] = riemannSolver( DOMAIN_DIR_W, pLeft, pRight, false );
	dCellBedElev = dCellBedElev_original;

	// Source term vector
	// TODO: Somehow get these sorted too...
	pSourceTerms.x = 0.0;
	pSourceTerms.y = -1 * GRAVITY * ( ( pNeigDataE.x + pNeigDataW.x ) / 2 ) * ( ( dNeigBedElevE - dNeigBedElevW ) / DOMAIN_DELTAX );
	pSourceTerms.z = -1 * GRAVITY * ( ( pNeigDataN.x + pNeigDataS.x ) / 2 ) * ( ( dNeigBedElevN - dNeigBedElevS ) / DOMAIN_DELTAY );


	// Calculation of change values per timestep and spatial dimension
	dDeltaValues.x	= ( pFlux[1].x  - pFlux[3].x  )/DOMAIN_DELTAX + 
					  ( pFlux[0].x  - pFlux[2].x  )/DOMAIN_DELTAY - 
					  pSourceTerms.x;
	dDeltaValues.z	= ( pFlux[1].y - pFlux[3].y )/DOMAIN_DELTAX + 
					  ( pFlux[0].y - pFlux[2].y )/DOMAIN_DELTAY - 
					  pSourceTerms.y;
	dDeltaValues.w	= ( pFlux[1].z - pFlux[3].z )/DOMAIN_DELTAX + 
					  ( pFlux[0].z - pFlux[2].z )/DOMAIN_DELTAY - 
					  pSourceTerms.z;

	// Round delta values to zero if small
	// TODO: Explore whether this can be rewritten as some form of clamp operation?
	if ( ( dDeltaValues.x > 0.0 && dDeltaValues.x <  VERY_SMALL ) ||
		 ( dDeltaValues.x < 0.0 && dDeltaValues.x > -VERY_SMALL ) ) 
		 dDeltaValues.x = 0.0;
	if ( ( dDeltaValues.z > 0.0 && dDeltaValues.z <  VERY_SMALL ) ||
		 ( dDeltaValues.z < 0.0 && dDeltaValues.z > -VERY_SMALL ) ) 
		 dDeltaValues.z = 0.0;
	if ( ( dDeltaValues.w > 0.0 && dDeltaValues.w <  VERY_SMALL ) ||
		 ( dDeltaValues.w < 0.0 && dDeltaValues.w > -VERY_SMALL ) ) 
		 dDeltaValues.w = 0.0;

	// Stopping conditions
	if ( ucStop > 0 )
	{
		pCellData.z = 0.0;
		pCellData.w = 0.0;
	}
	
	//if (ulIdx == 1057){
	//	printf("N: x: %f y: %f z: %f \n",pFlux[DOMAIN_DIR_N].x,pFlux[DOMAIN_DIR_N].y, pFlux[DOMAIN_DIR_N].z);
	//	printf("E: x: %f y: %f z: %f \n",pFlux[DOMAIN_DIR_E].x,pFlux[DOMAIN_DIR_E].y, pFlux[DOMAIN_DIR_E].z);
	//	printf("S: x: %f y: %f z: %f \n",pFlux[DOMAIN_DIR_S].x,pFlux[DOMAIN_DIR_S].y, pFlux[DOMAIN_DIR_S].z);
	//	printf("W: x: %f y: %f z: %f \n",pFlux[DOMAIN_DIR_W].x,pFlux[DOMAIN_DIR_W].y, pFlux[DOMAIN_DIR_W].z);
	//	printf("pSourceTerms in x: %f \n",pSourceTerms.y);
	//	printf("pSourceTerms in y: %f \n",pSourceTerms.z);
	//	printf("opt_h: %f opt_h_new: %f \n",pCellData.x, pCellData.x	- dLclTimestep * dDeltaValues.x);
	//	printf("vx: %f \n",   pCellData.z	- dLclTimestep * dDeltaValues.z);
	//	printf("vy: %f \n\n", pCellData.w	- dLclTimestep * dDeltaValues.w);
	//}

	// Update the flow state
	pCellData.x		= pCellData.x	- dLclTimestep * dDeltaValues.x;
	pCellData.z		= pCellData.z	- dLclTimestep * dDeltaValues.z;
	pCellData.w		= pCellData.w	- dLclTimestep * dDeltaValues.w;

	#ifdef FRICTION_ENABLED
	#ifdef FRICTION_IN_FLUX_KERNEL
	// Calculate the friction effects
	pCellData = implicitFriction(
		pCellData,
		dCellBedElev,
		dManningCoef,
		dLclTimestep
	);
	#endif
	#endif

	// New max FSL?
	if ( pCellData.x > pCellData.y && pCellData.y > -9990.0 )
		pCellData.y = pCellData.x;

	// Crazy low depths?
	if ( pCellData.x - dCellBedElev < VERY_SMALL )
		pCellData.x = dCellBedElev;

	// Commit to global memory
	pCellStateDst[ ulIdx ] = pCellData;
}

void poliniAdjuster(
	bool activated,
	cl_double cell_s,
	cl_double neig_s,
	cl_double dike_z,
	cl_double* cell_z,
	cl_double* neig_z,
	cl_double4* pneig,
	cl_uchar direction
){
	if(!activated){
		return;
	}

	if(*neig_z == -9999.0){
		*neig_z = 100;
		return;
		//return;
	}

	cl_double flowDepth;
	cl_double flowDepthNeigh;
	flowDepth = cell_s - dike_z;
	flowDepthNeigh = neig_s - dike_z;
	
	// Cell Lower / Neig Lower
	if (flowDepth <= 0.0 && flowDepthNeigh <= 0.0){
		*neig_z = dike_z;

	}
	// Cell Higher / Neig Lower
	if (flowDepth > 0.0 && flowDepthNeigh <= 0.0){
		//should neigh be also dike_z?
		*cell_z = dike_z;
	}
		
	// Cell Lower / Neig Higher
	if (flowDepth <= 0.0 && flowDepthNeigh > 0.0){
		*neig_z = dike_z;
	}
		
	// Cell Higher / Neig Higher
	if (flowDepth > 0.0 && flowDepthNeigh > 0.0){
		*cell_z = dike_z;
		*neig_z = dike_z;
	}
}


/*
 *  Calculate everything by also using LDS caching
 */
__kernel REQD_WG_SIZE_FULL_TS
void gts_cacheEnabled ( 
			__constant	cl_double *  				dTimestep,						// Timestep
			__global	cl_double const * restrict	dBedElevation,					// Bed elevation
			__global	cl_double4 *  			pCellStateSrc,					// Current cell state data
			__global	cl_double4 *  			pCellStateDst,					// Current cell state data
			__global	cl_double const * restrict	dManning						// Manning values
		)
{
	__local   cl_double4				lpCellState[ GTS_DIM1 ][ GTS_DIM2 ];			// Current cell state data (cache)

	// Identify the cell we're reconstructing (no overlap)
	__private cl_double					dLclTimestep;
	__private cl_long					lIdxX			= get_global_id(0) - get_group_id(0) * 2;
	__private cl_long					lIdxY			= get_global_id(1) - get_group_id(1) * 2;
	__private cl_long					lLocalX			= get_local_id(0);
	__private cl_long					lLocalY			= get_local_id(1);
	__private cl_ulong					lLocalSizeX		= get_local_size(0);
	__private cl_ulong					lLocalSizeY		= get_local_size(1);
	__private cl_double4				pCellData;
	__private cl_double					dManningCoef;
	__private cl_double					dCellBedElev;
	__private cl_ulong					ulIdx;
	__private cl_uchar					ucDirection;

	if ( lIdxX > DOMAIN_COLS - 1 || 
		 lIdxY > DOMAIN_ROWS - 1 || 
		 lIdxX < 0 || 
		 lIdxY < 0 )
	{
		// Ideally we'd just exit the function here, but then we wont reach the barrier
		lIdxX = max((long)0,min((long)(DOMAIN_COLS - 1),lIdxX));
		lIdxY = max((long)0,min((long)(DOMAIN_ROWS - 1),lIdxY));
	}

	ulIdx = getCellID(lIdxX, lIdxY);

	// The max FSL is substituted with the bed elevation, thereby reducing LDS consumption
	dLclTimestep							= *dTimestep;
	pCellData								= pCellStateSrc[ ulIdx ];
	dCellBedElev							= dBedElevation[ ulIdx ];
	dManningCoef							= dManning[ ulIdx ];
	lpCellState[ lLocalX ][ lLocalY ]		= pCellData;
	lpCellState[ lLocalX ][ lLocalY ].y		= dCellBedElev;

	#ifdef DEBUG_OUTPUT
	if ( lIdxX == DEBUG_CELLX && lIdxY == DEBUG_CELLY )
	{
		printf( "Work-item indices: { %u, %u )\n", get_global_id(0), get_global_id(1) );
		printf( "Cell index X:      { %u )\n", lIdxX );
		printf( "Cell index Y:      { %u )\n", lIdxY );
		printf( "WG index X:        { %u )\n", lLocalX );
		printf( "WG index Y:        { %u )\n", lLocalY );
		printf( "Linearised index:  { %u )\n", ulIdx );
		printf( "Local mem commit:  { %f, %f, %f, %f )\n", pCellData.x, dCellBedElev, pCellData.z, pCellData.w );
	}
	#endif

	barrier( CLK_LOCAL_MEM_FENCE );

	// Cell disabled?
	if ( pCellData.y <= -9999.0 || pCellData.x == -9999.0 )
	{
		pCellStateDst[ ulIdx ] = pCellData;
		return;
	}

	// Don't bother if we've gone beyond the domain bounds
	if ( lIdxX >= DOMAIN_COLS - 1 || 
		 lIdxY >= DOMAIN_ROWS - 1 || 
		 lIdxX <= 0 || 
		 lIdxY <= 0 ||
		 lLocalX >= lLocalSizeX - 1 ||
		 lLocalY >= lLocalSizeY - 1 ||
		 lLocalX <= 0 ||
		 lLocalY <= 0 ) 
		return;

	__private cl_double		dNeigBedElevN,dNeigBedElevE,dNeigBedElevS,dNeigBedElevW;
	__private cl_double4	pNeigDataN,pNeigDataE,pNeigDataS,pNeigDataW;							// Z, Zb, Qx, Qy
	__private cl_double4	pSourceTerms, dDeltaValues;												// Z, Qx, Qy
	__private cl_double4	pFlux[4];																// Z, Qx, Qy
	__private cl_double8	pLeft,				pRight;												// Z, H, Qx, Qy, U, V, Zb
	__private cl_uchar		ucStop			= 0;
	__private cl_uchar		ucDryCount		= 0;
	

	// Also don't bother if we've gone beyond the total simulation time
	if ( dLclTimestep <= 0.0 )
		return;

	pNeigDataW		= lpCellState[ lLocalX - 1 ][ lLocalY ];
	pNeigDataS		= lpCellState[ lLocalX ][ lLocalY - 1 ];
	pNeigDataE		= lpCellState[ lLocalX + 1 ][ lLocalY ];
	pNeigDataN		= lpCellState[ lLocalX ][ lLocalY + 1 ];
	dNeigBedElevW	= pNeigDataW.y;
	dNeigBedElevS	= pNeigDataS.y;
	dNeigBedElevE	= pNeigDataE.y;
	dNeigBedElevN	= pNeigDataN.y;

	#ifdef DEBUG_OUTPUT
	if ( lIdxX == DEBUG_CELLX && lIdxY == DEBUG_CELLY )
	{
		printf( "Current data:  { %f, %f, %f, %f )\n", pCellData.x, pCellData.y, pCellData.z, pCellData.w );
		printf( "Neighbour N:   { %f, %f, %f, %f )\n", pNeigDataN.x, pNeigDataN.y, pNeigDataN.z, pNeigDataN.w );
		printf( "Neighbour E:   { %f, %f, %f, %f )\n", pNeigDataE.x, pNeigDataE.y, pNeigDataE.z, pNeigDataE.w );
		printf( "Neighbour S:   { %f, %f, %f, %f )\n", pNeigDataS.x, pNeigDataS.y, pNeigDataS.z, pNeigDataS.w );
		printf( "Neighbour W:   { %f, %f, %f, %f )\n", pNeigDataW.x, pNeigDataW.y, pNeigDataW.z, pNeigDataW.w );
	}
	#endif

	if ( pCellData.x  - dCellBedElev  < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataN.x - dNeigBedElevN < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataE.x - dNeigBedElevE < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataS.x - dNeigBedElevS < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataW.x - dNeigBedElevW < VERY_SMALL ) ucDryCount++;

	// All neighbours are dry? Don't bother calculating
	if ( ucDryCount >= 5 ) return;

	// Reconstruct interfaces
	// -> North
	ucStop += reconstructInterface(
		pCellData,							// Left cell data
		dCellBedElev,						// Left bed elevation
		pNeigDataN,							// Right cell data
		dNeigBedElevN,						// Right bed elevation
		&pLeft,								// Output for left
		&pRight,							// Output for right
		DOMAIN_DIR_N
	);
	pNeigDataN.x  = pRight.S0;
	dNeigBedElevN = pRight.S6;
	pFlux[DOMAIN_DIR_N] = riemannSolver( DOMAIN_DIR_N, pLeft, pRight, false );

	// -> South
	ucStop += reconstructInterface(
		pNeigDataS,							// Left cell data
		dNeigBedElevS,						// Left bed elevation
		pCellData,							// Right cell data
		dCellBedElev,						// Right bed elevation
		&pLeft,								// Output for left
		&pRight,							// Output for right
		DOMAIN_DIR_S
	);
	pNeigDataS.x  = pLeft.S0;
	dNeigBedElevS = pLeft.S6;
	pFlux[DOMAIN_DIR_S] = riemannSolver( DOMAIN_DIR_S, pLeft, pRight, false );

	// -> East
	ucStop += reconstructInterface(
		pCellData,							// Left cell data
		dCellBedElev,						// Left bed elevation
		pNeigDataE,							// Right cell data
		dNeigBedElevE,						// Right bed elevation
		&pLeft,								// Output for left
		&pRight,							// Output for right
		DOMAIN_DIR_E
	);
	pNeigDataE.x  = pRight.S0;
	dNeigBedElevE = pRight.S6;
	pFlux[DOMAIN_DIR_E] = riemannSolver( DOMAIN_DIR_E, pLeft, pRight, false );

	// -> West
	ucStop += reconstructInterface(
		pNeigDataW,							// Left cell data
		dNeigBedElevW,						// Left bed elevation
		pCellData,							// Right cell data
		dCellBedElev,						// Right bed elevation
		&pLeft,								// Output for left
		&pRight,							// Output for right
		DOMAIN_DIR_W
	);
	pNeigDataW.x  = pLeft.S0;
	dNeigBedElevW = pLeft.S6;
	pFlux[DOMAIN_DIR_W] = riemannSolver( DOMAIN_DIR_W, pLeft, pRight, false );

	// Source term vector
	// TODO: Somehow get these sorted too...
	pSourceTerms.x = 0.0;
	pSourceTerms.y = -1 * GRAVITY * ( ( pNeigDataE.x + pNeigDataW.x ) / 2 ) * ( ( dNeigBedElevE - dNeigBedElevW ) / DOMAIN_DELTAX );
	pSourceTerms.z = -1 * GRAVITY * ( ( pNeigDataN.x + pNeigDataS.x ) / 2 ) * ( ( dNeigBedElevN - dNeigBedElevS ) / DOMAIN_DELTAY );

	// Calculation of change values per timestep and spatial dimension
	dDeltaValues.x	= ( pFlux[1].x  - pFlux[3].x  )/DOMAIN_DELTAX + 
					  ( pFlux[0].x  - pFlux[2].x  )/DOMAIN_DELTAY - 
					  pSourceTerms.x;
	dDeltaValues.z	= ( pFlux[1].y - pFlux[3].y )/DOMAIN_DELTAX + 
					  ( pFlux[0].y - pFlux[2].y )/DOMAIN_DELTAY - 
					  pSourceTerms.y;
	dDeltaValues.w	= ( pFlux[1].z - pFlux[3].z )/DOMAIN_DELTAX + 
					  ( pFlux[0].z - pFlux[2].z )/DOMAIN_DELTAY - 
					  pSourceTerms.z;

	// Round delta values to zero if small
	// TODO: Explore whether this can be rewritten as some form of clamp operation?
	if ( ( dDeltaValues.x > 0.0 && dDeltaValues.x <  VERY_SMALL ) ||
		 ( dDeltaValues.x < 0.0 && dDeltaValues.x > -VERY_SMALL ) ) 
		 dDeltaValues.x = 0.0;
	if ( ( dDeltaValues.z > 0.0 && dDeltaValues.z <  VERY_SMALL ) ||
		 ( dDeltaValues.z < 0.0 && dDeltaValues.z > -VERY_SMALL ) ) 
		 dDeltaValues.z = 0.0;
	if ( ( dDeltaValues.w > 0.0 && dDeltaValues.w <  VERY_SMALL ) ||
		 ( dDeltaValues.w < 0.0 && dDeltaValues.w > -VERY_SMALL ) ) 
		 dDeltaValues.w = 0.0;

	// Stopping conditions
	if ( ucStop > 0 )
	{
		pCellData.z = 0.0;
		pCellData.w = 0.0;
	}

	// Update the flow state
	pCellData.x		= pCellData.x	- dLclTimestep * dDeltaValues.x;
	pCellData.z		= pCellData.z	- dLclTimestep * dDeltaValues.z;
	pCellData.w		= pCellData.w	- dLclTimestep * dDeltaValues.w;

	#ifdef FRICTION_ENABLED
	#ifdef FRICTION_IN_FLUX_KERNEL
	// Calculate the friction effects
	pCellData = implicitFriction(
		pCellData,
		dCellBedElev,
		dManningCoef,
		dLclTimestep
	);
	#endif
	#endif

	// New max FSL?
	if ( pCellData.x > pCellData.y && pCellData.y > -9990.0 )
		pCellData.y = pCellData.x;

	// Crazy low depths?
	if ( pCellData.x - dCellBedElev < VERY_SMALL )
		pCellData.x = dCellBedElev;

	#ifdef DEBUG_OUTPUT
	if ( lIdxX == DEBUG_CELLX && lIdxY == DEBUG_CELLY )
	{
		printf( "Timestep:      { %f )\n", dLclTimestep );
		printf( "Reconstruct N: { %f, %f, %f, %f )\n", pNeigDataN.x, dNeigBedElevN, pNeigDataN.z, pNeigDataN.w );
		printf( "Reconstruct E: { %f, %f, %f, %f )\n", pNeigDataE.x, dNeigBedElevE, pNeigDataE.z, pNeigDataE.w );
		printf( "Reconstruct S: { %f, %f, %f, %f )\n", pNeigDataS.x, dNeigBedElevS, pNeigDataS.z, pNeigDataS.w );
		printf( "Reconstruct W: { %f, %f, %f, %f )\n", pNeigDataW.x, dNeigBedElevW, pNeigDataW.z, pNeigDataW.w );
		printf( "Delta terms:   { %f, %f, %f )\n", dDeltaValues.x, dDeltaValues.z, dDeltaValues.w );
		printf( "Source terms:  { %f, %f, %f )\n", pSourceTerms.x, pSourceTerms.y, pSourceTerms.z );
		printf( "New data:      { %f, %f, %f, %f )\n", pCellData.x, pCellData.y, pCellData.z, pCellData.w );
	}
	#endif

	// Commit to global memory
	pCellStateDst[ ulIdx ] = pCellData;
}
